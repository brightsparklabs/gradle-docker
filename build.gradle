/*
 * Created by brightSPARK Labs
 * www.brightsparklabs.com
 */

// -----------------------------------------------------------------------------
// PLUGINS
// -----------------------------------------------------------------------------

plugins {
    // Apply the Java Gradle plugin development plugin to add support for developing Gradle plugins.
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "1.3.1"
    id 'maven-publish'

    // Apply the Groovy plugin to add support for Groovy.
    id "groovy"
    // Apply the Java plugin to add support for Java.
    id "java"

    // Apply the Gradle-Baseline plugin for dependency checking.
    id "com.brightsparklabs.gradle.baseline" version "4.9.0"
}

shadowJar {
    archiveClassifier = null
}

// -----------------------------------------------------------------------------
// REPOSITORIES/DEPENDENCIES
// -----------------------------------------------------------------------------

repositories {
    mavenCentral()
}

// -----------------------------------------------------------------------------
// PROJECT IDENTIFICATION
// -----------------------------------------------------------------------------

project.description  = 'Gradle plugins for working with Docker'
project.group        = 'com.brightsparklabs'
project.version      = 'git describe --dirty'.execute().text.trim()

project.ext.scm      = 'https://github.com/brightsparklabs/gradle-docker.git'
project.ext.url      = 'https://github.com/brightsparklabs/gradle-docker'

def dockerImagePluginId = 'com.brightsparklabs.gradle.docker-image'

// -----------------------------------------------------------------------------
// CONFIGURATION: java-gradle-plugin
// -----------------------------------------------------------------------------

gradlePlugin {
    website = project.url
    vcsUrl = project.scm

    plugins {
        dockerImagePlugin {
            id = dockerImagePluginId
            implementationClass = 'com.brightsparklabs.gradle.docker.DockerImagePlugin'
            displayName = 'Gradle Docker Image'
            description = 'Simplifies working with Docker images'
            tags = [
                "brightsparklabs",
                "docker",
            ]
        }
    }
}
