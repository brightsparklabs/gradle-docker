/*
 * Created by brightSPARK Labs
 * www.brightsparklabs.com
 */

// -----------------------------------------------------------------------------
// PLUGINS
// -----------------------------------------------------------------------------

plugins {
    // Apply the Java Gradle plugin development plugin to add support for developing Gradle plugins.
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "0.19.0"
    id 'maven-publish'

    // Apply the Groovy plugin to add support for Groovy.
    id "groovy"
    // Apply the Java plugin to add support for Java.
    id "java"

    // Apply the Gradle-Baseline plugin for dependency checking.
    id "com.brightsparklabs.gradle.baseline" version "2.0.0"
}

// -----------------------------------------------------------------------------
// REPOSITORIES/DEPENDENCIES
// -----------------------------------------------------------------------------

repositories {
    mavenCentral()
}

// -----------------------------------------------------------------------------
// PROJECT IDENTIFICATION
// -----------------------------------------------------------------------------

project.description  = 'Gradle plugins for working with Docker'
project.group        = 'com.brightsparklabs'
project.version      = 'git describe --dirty'.execute().text.trim()

project.ext.scm      = 'https://github.com/brightsparklabs/gradle-docker.git'
project.ext.url      = 'https://github.com/brightsparklabs/gradle-docker'

def dockerImagePluginId = 'com.brightsparklabs.gradle.docker-image'

// -----------------------------------------------------------------------------
// CONFIGURATION: java-gradle-plugin
// -----------------------------------------------------------------------------

gradlePlugin {
    plugins {
        dockerImagePlugin {
            id = dockerImagePluginId
            implementationClass = 'com.brightsparklabs.gradle.docker.DockerImagePlugin'
        }
    }
}

// -----------------------------------------------------------------------------
// CONFIGURATION: com.gradle.plugin-publish
// -----------------------------------------------------------------------------

pluginBundle {
    website = project.url
    vcsUrl = project.scm
    tags = ['docker', 'image']

    plugins {
        dockerImagePlugin {
            displayName = 'Gradle Docker Image'
            description = 'Simplifies working with Docker images'
        }
    }
}

